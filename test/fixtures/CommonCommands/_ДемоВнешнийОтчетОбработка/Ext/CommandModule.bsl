#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	НачатьПомещениеФайла(Новый ОписаниеОповещения("ОбработкаКомандыПослеПомещенияФайла", ЭтотОбъект));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаКомандыПослеПомещенияФайла(Результат, Адрес, ВыбранноеИмяФайла, Контекст) Экспорт
	
	Если Не ЗначениеЗаполнено(Адрес) Тогда
		Возврат;
	КонецЕсли;
	
	СвойстваФайла = ОбщегоНазначенияКлиентСервер.РазложитьПолноеИмяФайла(ВыбранноеИмяФайла);
	
	Если НРег(СвойстваФайла.Расширение) = НРег(".epf") Тогда
		ЭтоВнешняяОбработка = Истина;
		
	ИначеЕсли НРег(СвойстваФайла.Расширение) = НРег(".erf") Тогда
		ЭтоВнешняяОбработка = Ложь;
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Выбранный файл не является внешним отчетом или обработкой.'"));
		Возврат;
	КонецЕсли;
	
	ИмяВнешнегоОбъекта = СоздатьВнешнийОтчетИлиОбработку(Адрес, ЭтоВнешняяОбработка, СвойстваФайла.Имя);
	
	Если ЭтоВнешняяОбработка Тогда
		ИмяФормы = "ВнешняяОбработка." + ИмяВнешнегоОбъекта + ".Форма";
	Иначе
		ИмяФормы = "ВнешнийОтчет." + ИмяВнешнегоОбъекта + ".Форма";
	КонецЕсли;
	
	ОткрытьФорму(ИмяФормы);
	
КонецПроцедуры

&НаСервере
Функция СоздатьВнешнийОтчетИлиОбработку(Адрес, ЭтоВнешняяОбработка, ИмяФайла)
	
	Если ЭтоВнешняяОбработка Тогда
		Менеджер = ВнешниеОбработки;
	Иначе
		Менеджер = ВнешниеОтчеты;
	КонецЕсли;
	
	ИмяВнешнегоОбъекта = Менеджер.Подключить(Адрес, , Ложь);
	Менеджер.Создать(ИмяВнешнегоОбъекта);
	
	Возврат ИмяВнешнегоОбъекта;
	
КонецФункции

#КонецОбласти
